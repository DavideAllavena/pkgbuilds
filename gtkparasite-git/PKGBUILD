# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Hagen Schink <troja84@googlemail.com>

pkgname=gtkparasite-git
pkgver=0.3.0.r95.445cd7a
pkgrel=1
pkgdesc="Gtk+ debugging and development tool"
url=http://chipx86.github.com/gtkparasite/
arch=(i686 x86_64)
license=(MIT)
depends=(pygtk gtksourceview3)
makedepends=(git)
options=(!libtool)
source=($pkgname::git://github.com/chipx86/${pkgname%-*}.git)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    echo $(awk -F , '/^AC_INIT/ {print $2}' configure.ac | tr -d '[ ]').r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd $pkgname/
    ./autogen.sh \
        PYTHON=python2 \
        --prefix=/usr
    make
}

package() {
    cd $pkgname/
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
} 
