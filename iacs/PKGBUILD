# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=iacs
pkgver=1.1.3.4260
pkgrel=1
pkgdesc="IBM i Access Client Solutions provides a Java based, platform-independent interface for IBM iAccess/iSeries (formerly known as AS400)."
arch=('any')
url="http://www.ibm.com/systems/power/software/i/access/solutions.html"
license=('custom')
depends=('java-runtime' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils')
install="${pkgname}.install"
source=("ftp://ftp.software.ibm.com/as400/products/clientaccess/solutions/updates/IBMiAccess_v1r1m3_Update2.zip"
        "ftp://ftp.software.ibm.com/as400/products/clientaccess/solutions/updates/Readme.txt"
        "ftp://ftp.software.ibm.com/as400/products/clientaccess/solutions/GettingStarted.txt"
        "${pkgname}"
        "${pkgname}.desktop")
md5sums=('5ce3742e7a7732ef41b6d5b695ec12e3'
         '6ca19bf71980c0e3928823a14f93276c'
         'e5b947452ec2e4f4813b3e7f44d6b42a'
         '70f8d4dd88fac48360fb648c24c67709'
         'b266c792d3b33fb2dad3c141bf7b0792')

package() {
  cd "${srcdir}"
  # Install documentation files
  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" "${srcdir}/GettingStarted.txt"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" "${srcdir}/Readme.txt"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" Documentation/*
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" Notices/*
  # Install license files
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" License/*.txt
  ln -s  LA_en.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install application files
  install -m 755 -d "${pkgdir}/usr/share/java/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" "acsbundle.jar"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" "AcsConfig.properties"
  # Install executable file
  install -m 755 -d "${pkgdir}/usr/bin"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}"
  # Install icons
  install -m 755 -d "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
  install -m 644 "Icons/logo32.png" "${pkgdir}/usr/share/icons/hicolor/32x32/apps/${pkgname}.png"
  install -m 755 -d "${pkgdir}/usr/share/icons/hicolor/128x128/apps"
  install -m 644 "Icons/logo128.png" "${pkgdir}/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"
  # Install desktop file
  install -m 755 -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${srcdir}/${pkgname}.desktop"
}

