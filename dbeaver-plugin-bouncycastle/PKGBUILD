# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-bouncycastle
pkgver=1.60.0
pkgrel=1
pkgdesc='Bouncy Castle library for DBeaver plugin for SSHv2 tunnels'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5')
source=("https://dbeaver.jkiss.org/update/sshj/latest/plugins/org.jkiss.bundle.bouncycastle_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('2aa877099b8485f92537a0cfae431eea31cecf0f4f181afdf28b6995c2fee527'
            'b0590245c1dd095140466efffb45478748b0c7086630261baece19cf2517323a')
noextract=("org.jkiss.bundle.bouncycastle_${pkgver}.jar")

package() {
  # Extract bundle
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.bouncycastle_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.bundle.bouncycastle_${pkgver}.jar" \
        -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.bouncycastle_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
