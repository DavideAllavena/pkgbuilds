# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=gtk3-icon-browser-git
_pkgname="${pkgname/-git}"
pkgver=20140626
pkgrel=1
pkgdesc="An application that shows GTK+ icons from current theme"
arch=(i686 x86_64)
url="https://git.gnome.org/browse/gtk+/tree/demos/icon-browser"
license=(LGPL)
depends=(gtk3 glib2 desktop-file-utils)
install="${pkgname}.install"
_plaingit="https://git.gnome.org/browse/gtk+/plain/demos/icon-browser"
source=("${_plaingit}/app-menu.ui"
        "${_plaingit}/gtk3-icon-browser.desktop"
        "${_plaingit}/iconbrowser.gresource.xml"
        "${_plaingit}/iconbrowserapp.c"
        "${_plaingit}/iconbrowserapp.h"
        "${_plaingit}/iconbrowserwin.c"
        "${_plaingit}/iconbrowserwin.h"
        "${_plaingit}/main.c"
        "${_plaingit}/window.ui"
        "nodisplay-false.patch")
md5sums=('b1d3969d14f4b3eb347fc8b21edd2bb8'
         'f292d786f604e6f71acca3c3e612e44b'
         '8aeea743bb686cbda2590359f6252dde'
         '0d8b9901d5e900e418531892fd6252b3'
         '5cf74bd59b647cdab25a23c10840f837'
         '564440385987af91fc27a964140c6664'
         'ea1e3859a9da54c93a7b54cef8129751'
         'f2bf90b296bfc1c8a2e211f795f11fe5'
         '05d36032d65ad4da3fcad0903d78da20'
         'ed8279d7ac71f0742bd33c87da56eead')

pkgver() {
  date +%Y%m%d
}

prepare() {
  cd "${srcdir}"
  cp -f "gtk3-icon-browser.desktop" "${_pkgname}.desktop.file"
  patch -i "nodisplay-false.patch"
}

build() {
  cd "${srcdir}"
  glib-compile-resources ./iconbrowser.gresource.xml --target=resources.c --sourcedir=. --generate-source
  gcc *.c *.h $(pkg-config --libs --cflags gtk+-3.0) -I . -o ${_pkgname}
}

package() {
  cd "${srcdir}"
  install -m 755 -D "${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -m 755 -D "${_pkgname}.desktop.file" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
}

