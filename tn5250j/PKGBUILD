# Maintainer: Muflone <webreg@vbsimple.net>
# Contributor: Gino Pezzin <pezzin@gmail.com>
pkgname=tn5250j
pkgver=0.6.3
pkgrel=6
pkgdesc="5250 terminal emulator for iSeries and AS/400 written in Java"
url="http://tn5250j.sourceforge.net/"
arch=('any')
license=('GPL')
depends=('bash' 'java-runtime' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils')
optdepends=('tn5250j-plugin-itext: to convert the spool texts in PDF'
            'tn5250j-plugin-jtopen: to export spool and data files'
            'tn5250j-plugin-jython: to execute Python scripts inside tn5250j'
            'tn5250j-plugin-kunststoff: to apply the Kunststoff look and feel'
            'tn5250j-plugin-log4j: to enable a more detailed logging'
            'tn5250j-plugin-mail: to send email messages for exported data or screenshots')
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.zip"
        "${pkgname}"
        "${pkgname}-16.png"
        "${pkgname}-32.png"
        "${pkgname}-48.png"
        "${pkgname}.desktop")
md5sums=('aca60d491902abde40b3d0a84a221daf'
         'f0f137499059c4b420f6c26b11d361d6'
         'f470d9d55ab2a15d08b149bbcba65386'
         '18ea177d7bedafb4b80d39a464b2f023'
         'a263a597708a121f27481f9f30fb0358'
         'ef4e6d672003f21493bc97f00e148b1d')
sha1sums=('676a06445b3a448ca15a38c99c77e4a37a7fff8c'
          '7739e527841c1ebe9987288322e7b2a5b27cf6c7'
          'a6b34f0651f68beeed59c24579421f4a16b39da2'
          '0a33c2b3713418931549367642b119df0969f117'
          '55b1f89ac61db1535a6f4b49c29edc4c5c9080dd'
          '9cffa8761334fdde7add6781e6f1a530d38349a2')
sha256sums=('a6d0216435ca5ea6bfc7f7b49cfd7656c7521e58772a2f3e6d4d0aa9844eac05'
            '5d75030edbba8a65762f38708b1e43e10e0253b3a526badad63eedc4243a70bd'
            'bd6b904448185a0b2e260271ab01de7f5c60214689001448adbc4b2d533d4211'
            '431e866b5ee383965ddfad5bb161c26f16d84219edbc9ca1ba3b7a7d0bc9901a'
            'c8364bc48e7b870b12f394bbd718d137e98e8a414a02f9e957552e915f89d3df'
            '309986e9178cc94a0b518e8b75b551336aeddf4fcdcdc81401dab82fa87bce42')
install="${pkgname}.install"
changelog="${pkgname}.changelog"

package() {
  # Install java package
  install -m755 -d "${pkgdir}/usr/share/java/${pkgname}"
  install -m644 -t "${pkgdir}/usr/share/java/${pkgname}" "tn5250j.jar"
  # Install icons
  for _size in 16 32 48
  do
    install -m644 -D "${pkgname}-${_size}.png" "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  # Install desktop file
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -t "${pkgdir}/usr/share/applications" "${pkgname}.desktop"
  # Install startup script
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -t "${pkgdir}/usr/bin" "${pkgname}"
  # Install documentation files
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "applet.txt" "CHANGELOG.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "CHANGELOG.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "e-mail.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "filetransfers.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "macros.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250App.html"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250AppPlugin.html"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250SSLApplet.php"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250SSLindex.php"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "readme.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "scripting.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "SSLinstructions.html"
}
