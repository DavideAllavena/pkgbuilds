# Maintainer: Muflone <webreg@vbsimple.net>
# Contributor: Gino Pezzin <pezzin@gmail.com>
pkgname=tn5250j
pkgver=0.7.1
pkgrel=1
pkgdesc="5250 terminal emulator for iSeries and AS/400 written in Java"
url="http://tn5250j.sourceforge.net/"
arch=('any')
license=('GPL')
depends=('bash' 'java-runtime' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils')
optdepends=('tn5250j-plugin-itext: to convert the spool texts in PDF'
            'tn5250j-plugin-jtopen: to export spool and data files'
            'tn5250j-plugin-jython: to execute Python scripts inside tn5250j'
            'tn5250j-plugin-kunststoff: to apply the Kunststoff look and feel'
            'tn5250j-plugin-log4j: to enable a more detailed logging'
            'tn5250j-plugin-mail: to send email messages for exported data or screenshots')
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}-no-depedencies.jar"
        "http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.zip"
        "${pkgname}"
        "${pkgname}.desktop")
md5sums=('06469f44decba77e58a509d48b301c72'
         '404bac575e7e6cbd5c8510cd7de99935'
         '1be2e2f7e5eb501f292dc0867a210160'
         'ef4e6d672003f21493bc97f00e148b1d')
sha1sums=('ede48ff98123acfb646124687e31e973e24ba2ab'
          'fe41aeeb3ef2206a404ca4646f7ffd81ae030109'
          '01e96d29242bf4dccc2e7a9290ab8e9cf78bd367'
          '9cffa8761334fdde7add6781e6f1a530d38349a2')
sha256sums=('a876eddbf9f74dd3a5a9d2410500a2cab23721247f70c3952fcfb8967ce697db'
            '2359b8b86f20e6acce9393deb0cf4ca169ead4b5da80ea6a4b370587f1640fae'
            '987cb9f101b8613b5787e50c1369e94f2678248e9a3ddc214e0690412cfa4405'
            '309986e9178cc94a0b518e8b75b551336aeddf4fcdcdc81401dab82fa87bce42')
noextract=("${pkgname}-${pkgver}-no-depedencies.jar")
install="${pkgname}.install"
changelog="${pkgname}.changelog"

prepare() {
  cd "${pkgname}-${pkgver}/resources"
  # Copy (as rename) some icons
  cp -f "tn5250j-128.png" "tn5250j-128x128.png"
  cp -f "tn5250j-256.png" "tn5250j-256x256.png"
  cp -f "tn5250j-512.png" "tn5250j-512x512.png"
}

package() {
  # Install desktop file
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -t "${pkgdir}/usr/share/applications" "${pkgname}.desktop"
  # Install startup script
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -t "${pkgdir}/usr/bin" "${pkgname}"
  # Install java packages
  install -m755 -d "${pkgdir}/usr/share/java/${pkgname}"
  install -m644 "${pkgname}-${pkgver}-no-depedencies.jar" "${pkgdir}/usr/share/java/${pkgname}/tn5250j.jar"

  cd "${pkgname}-${pkgver}/resources"
  # Install icons
  for _size in 16 32 48 128 256 512
  do
    install -m644 -D "${pkgname}-${_size}x${_size}.png" "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -m644 -D "${pkgname}-icon.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Install documentation files
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "applet.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "CHANGELOG.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "e-mail.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "filetransfers.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "java.policy"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "macros.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250App.html"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250AppPlugin.html"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250SSLApplet.php"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "My5250SSLindex.php"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "README.ssl"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "readme.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "scripting.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "SSLinstructions.html"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "tn5250jLaunchExternalProgram.txt"
  install -m644 -t "${pkgdir}/usr/share/doc/${pkgname}" "wishlist.txt"
}
