# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=freeoffice
pkgver=697
pkgrel=1
pkgdesc="A complete, reliable, lightning-fast and Microsoft Office-compatible office suite with a word processor, spreadsheet, and presentation graphics software."
arch=(i686 x86_64)
url="http://www.freeoffice.com/"
license=(custom)
depends=('gcc-libs' 'libxmu' 'libxrender' 'hicolor-icon-theme' 'xdg-utils' 'shared-mime-info' 'gtk-update-icon-cache' 'desktop-file-utils')
if [ "$CARCH" = 'x86_64' ]
then
  depends[0]='lib32-gcc-libs'
  depends[1]='lib32-libxmu'
  depends[2]='lib32-libxrender'
fi
install=${pkgname}.install
source=("http://www.softmaker.net/down/softmaker-${pkgname}-${pkgver}.tgz"
        "${pkgname}-textmaker"
        "${pkgname}-planmaker"
        "${pkgname}-presentations"
        "${pkgname}-textmaker.desktop"
        "${pkgname}-planmaker.desktop"
        "${pkgname}-presentations.desktop")
sha256sums=('cdfdc5294eef0774e45ffa3749cf869d6ba1651b48d173543f3eb1d3d6016220'
            '491577d73d1dad9e9b5b2e0e067881d8d291baf5f9825482996e9309c5ade422'
            'db9cb24a40d3025c89ae4b952b5ebf537661f731716f09099d915793406548c4'
            '81b29a332975a5b96da00d14627dbaafc1c7d1beefacce5e77bdb68f98c1a3c6'
            '1554213ee9058a6594e2b92e47891ad58e593a4ea7d678c755cd32e263f43da8'
            'c604ffe9df3beb41b5db8911162f2e3c895ffba19a710f3d8cc7da85e3c804c9'
            '2b305734e63c5fba12def3c6ddfa79ef711f1c857fe68250b0d03d56df2bd980')

build() {
  [ -d "${pkgname}" ] && rm -rf "${pkgname}"
  mkdir "${pkgname}"
  tar xz -f freeoffice.tgz -C "${pkgname}"
}

package() {
  msg2 "Installing package files..."
  install -d "${pkgdir}/usr/share"
  cp -r "${srcdir}/${pkgname}" "${pkgdir}/usr/share"
  msg2 "Installing license file..."
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/${pkgname}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  msg2 "Installing program executables..."
  install -d "${pkgdir}/usr/bin"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}-planmaker"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}-textmaker"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}-presentations"
  msg2 "Installing icons..."
  for size in 16 32 48
  do
    install -d "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps"
    ln -s "/usr/share/${pkgname}/icons/pml_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}-planmaker.png"
    ln -s "/usr/share/${pkgname}/icons/prl_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}-presentations.png"
    ln -s "/usr/share/${pkgname}/icons/tml_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}-textmaker.png"

    install -d "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes"
    ln -s "/usr/share/${pkgname}/icons/pmd_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-pmd.png"
    ln -s "/usr/share/${pkgname}/icons/prd_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-prd.png"
    ln -s "/usr/share/${pkgname}/icons/tmd_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-tmd.png"
  done
  msg2 "Installing desktop files..."
  install -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}-textmaker.desktop"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}-planmaker.desktop"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}-presentations.desktop"
  msg2 "Installing mime file..."
  install -d "${pkgdir}/usr/share/mime/packages"
  install -m 644 -t "${pkgdir}/usr/share/mime/packages" "${srcdir}/${pkgname}/mime/softmaker-freeoffice.xml"
}
