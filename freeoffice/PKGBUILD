# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=freeoffice
pkgver=690
pkgrel=1
pkgdesc="A complete, reliable, lightning-fast and Microsoft Office-compatible office suite with a word processor, spreadsheet, and presentation graphics software."
arch=(i686 x86_64)
url="http://www.freeoffice.com/"
license=(custom)
depends=(gcc-libs libxmu libxrender hicolor-icon-theme xdg-utils shared-mime-info gtk-update-icon-cache desktop-file-utils)
if [ "$CARCH" = 'x86_64' ]
then
  depends[0]='lib32-gcc-libs'
  depends[1]='lib32-libxmu'
  depends[2]='lib32-libxrender'
fi
install=${pkgname}.install
source=("http://www.softmaker.net/down/softmaker-${pkgname}-${pkgver}.tgz"
        "${pkgname}-textmaker"
        "${pkgname}-planmaker"
        "${pkgname}-presentations"
        "${pkgname}-textmaker.desktop"
        "${pkgname}-planmaker.desktop"
        "${pkgname}-presentations.desktop")
md5sums=('58717125c416c30fe138a0f62e0cfd98'
         '4e1508197f883ac878791f8d7a3c1ea9'
         '9e9ba712e7512780e54626f2df27a41d'
         '3d887a96e6118e53d394f1f03cb24fbc'
         '97d5a733c348f7c2a379894db966d8a9'
         '2be0b4f6555116b1b499f727004246ba'
         'c8562e474b6623686ba6d9b3f888bbc2')

build() {
  [ -d "${pkgname}" ] && rm -rf "${pkgname}"
  mkdir "${pkgname}"
  tar xz -f freeoffice.tgz -C "${pkgname}"
}

package() {
  msg2 "Installing package files..."
  install -d "${pkgdir}/usr/share"
  cp -r "${srcdir}/${pkgname}" "${pkgdir}/usr/share"
  msg2 "Installing license file..."
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/${pkgname}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  msg2 "Installing program executables..."
  install -d "${pkgdir}/usr/bin"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}-planmaker"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}-textmaker"
  install -m 755 -t "${pkgdir}/usr/bin" "${srcdir}/${pkgname}-presentations"
  msg2 "Installing icons..."
  for size in 16 32 48
  do
    install -d "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps"
    ln -s "/usr/share/${pkgname}/icons/pml_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}-planmaker.png"
    ln -s "/usr/share/${pkgname}/icons/prl_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}-presentations.png"
    ln -s "/usr/share/${pkgname}/icons/tml_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}-textmaker.png"

    install -d "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes"
    ln -s "/usr/share/${pkgname}/icons/pmd_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-pmd.png"
    ln -s "/usr/share/${pkgname}/icons/prd_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-prd.png"
    ln -s "/usr/share/${pkgname}/icons/tmd_${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-tmd.png"
  done
  msg2 "Installing desktop files..."
  install -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}-textmaker.desktop"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}-planmaker.desktop"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}-presentations.desktop"
  msg2 "Installing mime file..."
  install -d "${pkgdir}/usr/share/mime/packages"
  install -m 644 -t "${pkgdir}/usr/share/mime/packages" "${srcdir}/${pkgname}/mime/softmaker-freeoffice.xml"
}
