# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=vmware-ovftool
pkgver=3.5.0.1274719
pkgrel=1
pkgdesc="VMware Open Virtualization Format tool"
arch=('i686' 'x86_64')
url="https://www.vmware.com/support/developer/ovf/"
license=('custom:vmware')
depends=('curl' 'libxml2')
_pkgmainver=${pkgver%.*}
_pkgsubver=${pkgver##*.}

if [ "$CARCH" = "x86_64" ]
then
  _pkgarch=$CARCH
  sha256sums=('e11e7c726658f6c41654d7e0ef1b5b2d45aa436e9f36ac39a994aaf70f343c4f')
else
  _pkgarch=i386
  sha256sums=('cc58e04aef3b608795cc788a196be6786bf173c2b2c05ffbea16ff50fbad68c8')
fi
source=("file://VMware-ovftool-${_pkgmainver}-${_pkgsubver}-lin.${_pkgarch}.bundle")

build() {
  rm -rf "${srcdir}/build"
  sh "../VMware-ovftool-${_pkgmainver}-${_pkgsubver}-lin.${_pkgarch}.bundle" -x "${srcdir}/build"
}

package() {
  cd "${srcdir}/build/${pkgname}"
  # Install binaries files
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgname}" icudt44l.dat lib*
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}" ovftool ovftool.bin
  # Install data files
  for _subdir in certs env env/en schemas/DMTF schemas/vmware
  do
    install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/${_subdir}"
    install -m 644 -t "${pkgdir}/usr/lib/${pkgname}/${_subdir}" "${_subdir}"/*.*
  done
  # Install main script symlink
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/ovftool" "${pkgdir}/usr/bin/ovftool"
  # Install license files
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    vmware.eula vmware-eula.rtf open_source_licenses.txt
  # Install documentation files
  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.txt
}

