# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=2.3.0
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='http://code.google.com/p/spyderlib/'
license=('MIT')
install="${pkgname}.install"
depends=('python2-pyqt4' 'python2-pyflakes' 'desktop-file-utils' 'gtk-update-icon-cache' 'python2-pyzmq' 'python2-pygments')
makedepends=('python2-sphinx' 'python2-setuptools')
optdepends=('python2-pylint: powerful code analysis'
            'ipython2: enhanced Python interpreter'
            'python2-rope: editor code completion, calltips and go-to-definition'
            'python2-sphinx: rich text help on the object inspector'
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-psutil: memory/CPU usage in the status bar'
            'python2-h5py: HDF5 support'
            'python2-matplotlib: interactive 2D/3D data plotting'
            'pep8-python2: real-time code style analysis'
            'python2-sympy: symbolic mathematics for the IPython console')
source=("https://bitbucket.org/spyder-ide/spyderlib/downloads/${pkgname}-${pkgver}.zip"
        "pylint.patch"
        "python2.patch")
sha256sums=('4a64159eda895c4e1fde92a05ebfcc76f98d9d8d39317d2ffe68ed596635e8f6'
            '3c86d8921f9613090d5f7f99129247b2689bc12e8ab84894623f6f6c1fcd6bdb'
            'd2238710785f9967c31d4d4eefe63711022a7180db75c8832edd1e21cf2971b6')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "../pylint.patch"
  patch -p1 -i "../python2.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
