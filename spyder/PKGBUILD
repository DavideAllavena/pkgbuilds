# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=4.1.3
pkgrel=2
pkgdesc="The Scientific Python Development Environment"
arch=(any)
url="https://www.spyder-ide.org/"
license=(MIT)
makedepends=(python-setuptools python-sphinx)
depends=(
    python-atomicwrites
    python-chardet
    python-cloudpickle
    python-diff-match-patch
    python-intervaltree
    ipython
    python-jedi
    python-keyring
    jupyter-nbconvert
    python-numpydoc
    python-parso
    python-pexpect
    python-pickleshare
    python-psutil
    python-pygments
    python-pylint
    python-pyqt5
    python-pyqtwebengine
    qt5-webkit
    python-language-server
    python-xdg
    python-pyzmq
    python-qdarkstyle
    python-qtawesome
    python-qtconsole
    python-qtpy
    python-sphinx
    python-spyder-kernels
    python-watchdog
)
optdepends=(
    'cython: run Cython files in the IPython Console'
    'python-matplotlib: 2D/3D plotting in the IPython Console'
    'python-numpy: support for N-dimensional arrays in the Variable Explorer'
    'python-pandas: support for DataFrames and Series in the Variable Explorer'
    'python-scipy: support for Matlab workspace in the Variable Explorer'
    'python-sympy: symbolic mathematics in the IPython Console'
)
checkdepends=(
    python-pytest
    python-pytest-qt
    python-pytest-mock
    python-pytest-cov
    python-pytest-xvfb
    python-pytest-ordering
    python-pytest-lazy-fixture
    python-mock
    python-flaky
    python-pandas
    python-scipy
    python-sympy
    python-pillow
    python-matplotlib
    cython
    git
    tk
)
source=(https://github.com/spyder-ide/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        spyder-ignore-deps-tests.patch::https://github.com/spyder-ide/spyder/commit/833de4b928a5d2b28422d2c56a08e9217af6ad2a.patch
        spyder3_to_spyder.patch)
sha256sums=('a55f5323c2be2813f52a5941ed65eb98f5bb81cc31df464374965d389ba24fd9'
            '426d1228a52c02ce77dcf3904ca07c024a0f23f019e79984b9563b47bf9164d1'
            '42b51d2696f06e4ad69be7379e913fd90e5c09c3e8244b6f38ef127ae86cfe89')

prepare() {
  cd ${pkgname}-${pkgver}
  # Replace spyder3 with spyder
  patch -p1 -i ../spyder3_to_spyder.patch
  mv scripts/spyder{3,}.desktop
  mv scripts/spyder{3,}.appdata.xml
  # Allow our jedi and parso versions
  sed -i "s|JEDI_REQVER = '=0.15.2'|JEDI_REQVER = '>=0.15.2'|" -i spyder/dependencies.py
  sed -i "s|PARSO_REQVER = '=0.5.2'|PARSO_REQVER = '>=0.5.2'|" -i spyder/dependencies.py
  sed -i "s|PYLS_REQVER = '>=0.31.9;<0.32.0'|PYLS_REQVER = '>=0.31.9'|" -i spyder/dependencies.py
  # Fix test failure in uneeded subtests
  patch -p1 -i ../spyder-ignore-deps-tests.patch
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  # Required per above patching
  sed -i "s|jedi==0.15.2|jedi>=0.15.2|" -i setup.py
  sed -i "s|parso==0.5.2|parso>=0.5.2|" -i setup.py
  sed -i "s|python-language-server[all]>=0.31.9,<0.32.0|python-language-server[all]>=0.31.9|" -i setup.py
  sed -i "s|jedi =0.15.2|jedi >=0.15.2|" -i requirements/conda.txt
  sed -i "s|parso =0.5.2|parso >=0.5.2|" -i requirements/conda.txt
  sed -i "s|jedi =0.15.2|jedi >=0.15.2|" -i binder/environment.yml
  sed -i "s|parso =0.5.2|parso >=0.5.2|" -i binder/environment.yml
  sed -i "s|python-language-server >=0.31.9,<0.32.0|python-language-server >=0.31.9|" -i binder/environment.yml
  # Some tests failures but upstream does not support us
  python runtests.py || warning "Tests failed"
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
  # Install icon file
  install -Dm644 spyder/images/spyder.svg -t "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}"/usr/bin/spyder_win_post_install.py
}
