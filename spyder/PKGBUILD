# Maintainer: Muflone http://url.muflone.com/contacts
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=2.2.5
pkgrel=2
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='http://code.google.com/p/spyderlib/'
license=('MIT')
install="${pkgname}.install"
depends=('python2-pyqt4' 'python2-pyflakes' 'desktop-file-utils' 'gtk-update-icon-cache' 'python2-pyzmq' 'python2-pygments')
makedepends=('python2-sphinx' 'python2-setuptools')
optdepends=('python2-pylint: powerful code analysis'
            'ipython2: enhanced Python interpreter'
            'python2-rope: editor code completion, calltips and go-to-definition'
            'python2-sphinx: rich text help on the object inspector'
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-psutil: memory/CPU usage in the status bar'
            'python2-h5py: HDF5 support'
            'python2-matplotlib: interactive 2D/3D data plotting'
            'pep8-python2: real-time code style analysis'
            'python2-sympy: symbolic mathematics for the IPython console')
source=("http://spyderlib.googlecode.com/files/${pkgname}-${pkgver}.zip" 
        "pylint.patch")
md5sums=('1c9aa650dae9f883616e917803f8a3be'
         '3fe95915036634841b619670ad947e96')

prepare() {
  patch -p0 < "pylint.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
