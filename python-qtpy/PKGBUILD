# $Id: PKGBUILD 342293 2018-06-10 20:54:36Z muflone $
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgbase='python-qtpy'
_pkgbase='qtpy'
pkgname=('python-qtpy' 'python2-qtpy')
pkgver=1.4.1
pkgrel=1
pkgdesc='Provides an uniform layer to support PyQt5, PyQt4 and PySide with a single codebase'
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
arch=('any')
url="https://github.com/spyder-ide/${_pkgbase}/"
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('b8118f8a9198e4073447e0bc70455dc4e88aa036b5e75dbed7940dc012b66ae9')

package_python2-qtpy() {
  depends=('python2')
  optdepends=('python2-pyqt4: Qt4 Python bindings'
              'python2-pyqt5: Qt5 Python bindings'
              'python2-pyside: PySide Qt4 Python bindings')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-qtpy() {
  depends=('python')
  optdepends=('python-pyqt4: Qt4 Python bindings'
              'python-pyqt5: Qt5 Python bindings'
              'python-pyside: PySide Qt4 Python bindings')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
