# Maintainer: Muflone <muflone@vbsimple.net>

pkgname=remmina-plugin-open
pkgver=1.0.0.1
pkgrel=2
_builderver=1.0.0.3
pkgdesc="A protocol plugin for Remmina to open a document with its associated application."
arch=(i686 x86_64)
url="https://github.com/muflone/remmina-plugin-open"
license=('GPL')
depends=('gtk3' 'remmina' 'hicolor-icon-theme')
makedepends=('pkg-config' 'cmake')
optdepends=(
  'xdg-utils: for Automatically detected option'
  'libgnome: for Open for GNOME option'
)
install=${pkgname}.install
source=(
  "remmina-plugin-builder_${_builderver}.tar.gz::https://github.com/muflone/remmina-plugin-builder/archive/${_builderver}.tar.gz"
  "${pkgname}_${pkgver}.tar.gz::https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz"
)
md5sums=(
  'c74ca55ae6ef92f2706acad3a35511de'
  'c6b1d16d057790552d19c2b23c4da9bf'
)

build() {
  [ -d build ] && rm -rf build
  cp -r remmina-plugin-builder-${_builderver} build
  cp -r "${pkgname}-${pkgver}"/* build/remmina-plugin-to-build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd ${srcdir}/build/
  make DESTDIR=${pkgdir} install
}
