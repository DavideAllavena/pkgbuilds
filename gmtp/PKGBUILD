# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: yugrotavele <yugrotavele at archlinux dot us>

pkgname=gmtp
pkgver=1.3.6
pkgrel=2
pkgdesc="A simple MP3 player client for MTP based devices"
arch=('i686' 'x86_64')
url="http://gmtp.sourceforge.net/"
license=('BSD')
depends=('dconf' 'flac' 'gtk3' 'libmtp' 'libid3tag' 'libusb' 'libvorbis' 'desktop-file-utils')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz"::"http://sourceforge.net/projects/gmtp/files/gMTP-${pkgver}/${pkgname}-${pkgver}.tar.gz/download")
md5sums=('e00439b6347fd092cf1caa9c1ef9f751')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  make DESTDIR="${pkgdir}" install
}
