# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
pkgname=python-${_pkg}
pkgver=0.5.2
pkgrel=4
pkgdesc="Jupyter Kernels for the Spyder console"
arch=(any)
url="https://github.com/spyder-ide/spyder-kernels/"
license=(MIT)
makedepends=(python python-setuptools)
depends=(
    python-ipykernel
    python-pyzmq
    python-jupyter_client
    python-cloudpickle
    python-wurlitzer
)
checkdepends=(
    python-numpy
    python-pandas
    python-scipy
    python-pytest
    python-flaky
    python-mock
    cython
    python-matplotlib
    python-xarray
)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
       spyder-kernels-old-fix-ipython-7.10-test.patch::https://github.com/spyder-ide/spyder-kernels/commit/d8b53db479df3a105ee3b908ef4af34806bcd57d.patch)
sha256sums=('861b7547ef407e1a02f315a9f54fe55fe21b516e6d40b7be0df524a85baf664e'
            '7568826e89ab217d6c33f84f0325ce01d9f683a913f5aee9785143a556216c1d')

prepare() {
  cd ${_pkg}-${pkgver}
  patch -p1 -i ../spyder-kernels-old-fix-ipython-7.10-test.patch
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest spyder_kernels
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

