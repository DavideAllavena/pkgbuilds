# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgbase='python-spyder-kernels'
_pkgbase='spyder-kernels'
pkgname=('python-spyder-kernels' 'python2-spyder-kernels')
pkgver=0.4.4
pkgrel=1
pkgdesc='Jupyter Kernels for the Spyder console'
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
arch=('any')
url="https://github.com/spyder-ide/${_pkgbase}/"
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('5ec65634e5b999b71a8542df30fa6e609b21637c1e6cc0867954105a8e667e98')

prepare() {
  # Prepare sources for Python 2.x
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/python2-${_pkgbase}-${pkgver}"
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/python2-${_pkgbase}-${pkgver}"
  python setup.py build
}

package_python-spyder-kernels() {
  depends=('python-ipykernel' 'python-cloudpickle')

  cd "${_pkgbase}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m 644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
package_python2-spyder-kernels() {
  depends=('python2-ipykernel' 'python2-cloudpickle')

  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m 644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

