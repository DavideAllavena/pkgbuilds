# $Id: PKGBUILD 325668 2018-05-20 22:12:38Z muflone $
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Arne Hoch <arne@derhoch.de>

pkgname=dbeaver
pkgver=5.0.2
pkgrel=1
pkgdesc="Free universal SQL Client for developers and database administrators (community edition)"
arch=('x86_64')
url="http://dbeaver.jkiss.org/"
license=("Apache")
depends=('java-runtime=8' 'gtk2' 'gtk-update-icon-cache')
makedepends=('maven' 'java-environment=8')
optdepends=('dbeaver-plugin-office: export data in Microsoft Office Excel format'
            'dbeaver-plugin-svg-format: save diagrams in SVG format')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/serge-rider/dbeaver/archive/${pkgver}.tar.gz"
        "bug_3247_missing_sshj_1.patch"::"https://github.com/dbeaver/dbeaver/commit/c2128565e44480cb0600c7d49b988aae3921027b.patch"
        "bug_3247_missing_sshj_2.patch"::"https://github.com/dbeaver/dbeaver/commit/81e070287902f2b9b7b46262fc91b0ea2e6d6e76.patch"
        "bug_3247_missing_sshj_3.patch"::"https://github.com/dbeaver/dbeaver/commit/482f81bff7930ec4d7358f561063b05992e56b7d.patch"
        "bug_3247_missing_sshj_4.patch"::"https://github.com/dbeaver/dbeaver/commit/9c83516b4e8ca83fc02fbd220c6fb56f63fd2995.patch"
        "${pkgname}.desktop"
        "${pkgname}.profile.gz"
        "${pkgname}.hook"
        "dbeaver.install")
sha256sums=('094565e159a430567f07c1f03110894e4b126f3bad068c81b03792d037ba5b4c'
            '96ac5769696fa3ff997d26a093c5f3b7c3f575d5e5db1040baff9b9cfb812e8e'
            'ffaef6449dd4baeef95f2bfef7abc7ad9d4bc74f66395bacbc4f5ef613858584'
            '85b085bf7a177d1218652da0af277cb9e8996eb36aa435fcc2496e91a92c6b4e'
            'eeb76c2932669a8cccd626a9567891f616814db8167acc09ed79f8668c0d4fde'
            'd1365a1e1a9a7b3949fc1b9e9928e93b7d8b3d04b5d0eee7c37eaaa362466c47'
            '29b9b2f32bcdf33b14680b4582e95c42cb5109c2168c741278630b68d1f265ee'
            'b035ea82c5c349cf992a411f208d8c874c2844d2f8490e4ad21cafb5be3fbfba'
            'f8d65dd933049b587a5815ea75a30ef944300b812df383ca1c2dcd68280bc7ab')
install="${pkgname}.install"

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -N -p 1 -i "${srcdir}/bug_3247_missing_sshj_1.patch"
  patch -N -p 1 -i "${srcdir}/bug_3247_missing_sshj_2.patch"
  patch -N -p 1 -i "${srcdir}/bug_3247_missing_sshj_3.patch"
  patch -N -p 1 -i "${srcdir}/bug_3247_missing_sshj_4.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  export MAVEN_OPTS="-Xmx2048m"
  mvn --batch-mode package
}

package() {
  cd "${pkgname}-${pkgver}/product/standalone"
  # Install icons into /usr/share/icons/hicolor
  for _size in 16 32 48 64 128 256 512
  do
    install -m 644 -D "icons-sources/icon_${_size}x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/dbeaver.png"
  done

  # Set system architecture
  if [ "${CARCH}" = 'x86_64' ]; then
    _arch="x86_64"
  else
    _arch='x86'
  fi

  # Move into the target directory
  cd "target/products/org.jkiss.dbeaver.core.product/linux/gtk/${_arch}"

  # Initially install everything into /usr/lib/dbeaver
  install -m 755 -d "${pkgdir}/usr/lib"
  cp -r "dbeaver" "${pkgdir}/usr/lib/${pkgname}"

  # Move shared data to /usr/share/dbeaver
  cd "${pkgdir}/usr/lib/${pkgname}"
  install -m 755 -d "${pkgdir}/usr/share/${pkgname}"
  for _file in configuration features p2 .eclipseproduct artifacts.xml dbeaver.ini readme.txt
  do
    mv "${_file}" "${pkgdir}/usr/share/${pkgname}"
    ln -s "/usr/share/${pkgname}/${_file}" .
  done

  # Install additional licenses
  install -m 755 -d "${pkgdir}/usr/share/licenses"
  mv licenses "${pkgdir}/usr/share/licenses/${pkgname}"

  # Install icons
  install -m 755 -d "${pkgdir}/usr/share/pixmaps"
  mv dbeaver.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  mv icon.xpm "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"

  # Install executable files into /usr/bin
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install application launcher into /usr/share/applications
  install -m 755 -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${srcdir}/${pkgname}.desktop"

  # Clean up and install new profile
  rm -rf "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.core"
  cd "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.engine/profileRegistry/DefaultProfile.profile"
  find . -name "*.profile.gz" -delete
  install -m 644 "${srcdir}/${pkgname}.profile.gz" "1502633007017.profile.gz"
  cd "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.engine"
  rm -f ".settings/org.eclipse.equinox.p2.artifact.repository.prefs"
  rm ".settings/org.eclipse.equinox.p2.metadata.repository.prefs"
  rmdir ".settings"

  # Install system hook
  install -m 755 -d "${pkgdir}/usr/share/libalpm/hooks"
  install -m 644 "${srcdir}/${pkgname}.hook" "${pkgdir}/usr/share/libalpm/hooks"

  # Create configuration file (handled by the hook)
  cd "${pkgdir}/usr/share/dbeaver/configuration/org.eclipse.equinox.simpleconfigurator"
  install -m 755 -d "${pkgdir}/etc/${pkgname}/bundles.d"
  mv "bundles.info" "${pkgdir}/etc/${pkgname}/bundles.d/00-${pkgname}.info"
  ln -s "/etc/${pkgname}/bundles.info" .
}
