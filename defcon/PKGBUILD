# Maintainer: Muflone <webreg@vbsimple.net>
# Contributor: Tony Dodd <arch@recalcitrance.net>
# Contributor: Jonathan Frazier <eyeswide@gmail.com>
pkgname=defcon
pkgver=1.42
pkgrel=2
pkgdesc="Global Thermonuclear War game"
arch=('i686' 'x86_64')
url="http://www.introversion.co.uk/defcon/"
license=("custom")
depends=('glu' 'mesa' 'sdl' 'alsa-plugins' 'libpulse'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
if [ "${CARCH}" = "x86_64" ]
then
  depends[0]='lib32-glu'
  depends[1]='lib32-mesa'
  depends[2]='lib32-sdl'
  depends[3]='lib32-alsa-plugins'
  depends[4]='lib32-libpulse'
fi
source=("http://download.introversion.co.uk/defcon/linux/${pkgname}-v${pkgver}.tar.gz"
        "${pkgname}-16.png"
        "${pkgname}-32.png"
        "${pkgname}-48.png"
        "${pkgname}-128.png"
        "${pkgname}.desktop")
md5sums=('df7ee425ebfa88615efc26508564feca'
         '5fe8db2ddd9d8b729bbdc45ef8ed952e'
         '9f644374d718126ddf5a1b8b6b4bf25e'
         'ba986cfe6c1cdfb2a415c0c551c840e2'
         'a75d3a0b29668e3ebd2afcaec5d2b199'
         'e470f70aa1c72f6e37b77e27d769da3f')
install="${pkgname}.install"

package() {
  # install desktop file and icon
  install -m 755 -D "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -m 644 -D "${pkgname}-16.png" "$pkgdir/usr/share/icons/hicolor/16x16/apps/${pkgname}.png"
  install -m 644 -D "${pkgname}-32.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/${pkgname}.png"
  install -m 644 -D "${pkgname}-48.png" "$pkgdir/usr/share/icons/hicolor/48x48/apps/${pkgname}.png"
  install -m 644 -D "${pkgname}-128.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"

  cd "${pkgname}-v${pkgver}"
  
  #install program files
  install -m 755 -D "lib/defcon.bin.x86" "${pkgdir}/usr/share/${pkgname}/defcon"
  install -m 644 -t "${pkgdir}/usr/share/${pkgname}" "manual.pdf"
  install -m 644 -t "${pkgdir}/usr/share/${pkgname}" "doc/linux.txt"
  install -m 755 -t "${pkgdir}/usr/share/${pkgname}" "lib/open-www.sh"
  install -m 755 -t "${pkgdir}/usr/share/${pkgname}" "lib/main.dat"
  install -m 755 -t "${pkgdir}/usr/share/${pkgname}" "lib/sounds.dat"

  #install runscript to /usr/bin
  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/share/${pkgname}/defcon" "${pkgdir}/usr/bin/${pkgname}"

  # Install license
  install -D -m 644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
