# Maintainer: Muflone <muflone@vbsimple.net>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=gespeaker
pkgver=0.8.1
pkgrel=8
pkgdesc="A GTK+ frontend for espeak and mbrola to speech the read text."
url="http://code.google.com/p/gespeaker/"
arch=('any')
license=('GPL2')
depends=('espeak' 'librsvg' 'pygtk' 'python-dbus-common' 'python2-xdg')
optdepends=('mbrola: for enhanced mbrola voices support'
            'mbrola-voices: at least one mbrola voice')
provides=('gespeaker')
source=("http://www.muflone.com/projects/gespeaker/archive/gespeaker_${pkgver}_all.tar.gz")
md5sums=('8849c147d6e15d1dd15866bcd4b1aa3a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Python2 fix
  sed -i 's#env python#env python2#' setup.py
  sed -i 's#env python#env python2#' gespeaker
  sed -i 's#env python#env python2#' src/gespeaker.py
  sed -i 's#env python#env python2#' plugins/plugin_kopete/gespeaker_kopete.py
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root "${pkgdir}"
}

