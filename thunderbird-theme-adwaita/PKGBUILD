# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=thunderbird-theme-adwaita
pkgver=24.0
pkgrel=1
pkgdesc="Adwaita theme for Thunderbird (Matches the default Gnome Shell theme)"
url="https://addons.mozilla.org/en-US/thunderbird/addon/adwaita-thunderbird"
arch=('any')
license=('MPL')
depends=("thunderbird")
makedepends=('unzip')
_git_commit="97431b7dedf4f856b6e7d66faa2dc5c1371bfb27"
source=("${pkgname}-${pkgver}.zip"::"https://github.com/gnome-integration-team/thunderbird-gnome/archive/${_git_commit}.zip")
md5sums=('190466c20cb0a0cc49f4b9ff6076abe7')

package() {
  cd "${srcdir}/thunderbird-gnome-${_git_commit}/theme"
  # Find extension UUID
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  # Prepare destination folder
  _dstdir="${pkgdir}/usr/lib/thunderbird/extensions/${_emid}"
  install -d "${_dstdir}"
  # Copy files
  cp -R * "${_dstdir}"
  # Restore standard permissions
  find "${pkgdir}" -type d -print0 | xargs --null chmod 0755
  find "${pkgdir}" -type f -print0 | xargs --null chmod 0644
}


