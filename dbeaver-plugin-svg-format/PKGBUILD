# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-svg-format
pkgver=1.0.36.201901062110
pkgrel=1
pkgdesc='DBeaver plugin to save diagrams in SVG format'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-batik')
source=("https://dbeaver.jkiss.org/update/svg/latest/plugins/org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
        "https://dbeaver.jkiss.org/update/svg/latest/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('7c4a0df7f15c1de02f3ea96155bb8e30fc5393694e605cafea1d41c7e9c65f36'
            '375a7fd4c037cedaf92a571560b4295c5d29ed9f5cd64f30292c62bc033e56d6'
            'f67da6850c0a5d273083d68e28b1d31dcdc0819aa6d19c28d2c90db8124831b3')
noextract=("org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
           "ui.svg.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
