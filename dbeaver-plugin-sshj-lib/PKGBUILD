# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj-lib
pkgver=0.26.0
pkgrel=1
pkgdesc='SSHv2 library for Java for DBeaver plugin for SSHv2 tunnels'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-bouncycastle')
source=("https://dbeaver.jkiss.org/update/sshj/latest/plugins/org.jkiss.bundle.sshj_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('f9b0fe1cf70c8d932f55b2bc48909e413c685678df5d62eff25aa66e48de9a93'
            '66c4bbab8076f090927df60002e038c56de479bc2557bfb703c0b23b28efcc2b')
noextract=("org.jkiss.bundle.sshj_${pkgver}.jar")

package() {
  # Extract bundle
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.sshj_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.bundle.sshj_${pkgver}.jar" \
        -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.sshj_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
