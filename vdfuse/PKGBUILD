# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: laloch <lalochcz@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=vdfuse
pkgver=82a
pkgrel=8
_headers=svn-47049
pkgdesc="A FUSE module for mounting VirtualBox disk images (VDI/VMDK/VHD) on the host"
arch=('i686' 'x86_64')
url="http://forums.virtualbox.org/viewtopic.php?f=26&t=33355"
license=('GPL3')
depends=('fuse' 'virtualbox' 'virtualbox-sdk')
makedepends=('pkgconfig')
install="${pkgname}.install"
source=("http://ftp.de.debian.org/debian/pool/main/v/virtualbox/virtualbox_4.1.18-dfsg-2+deb7u3.debian.tar.gz"
        "https://github.com/muflone/virtualbox-includes/archive/${_headers}.tar.gz"
        "vdautomount-0.1.py"::"http://forums.virtualbox.org/download/file.php?id=2865"
        "add-typedef-to-PARTITIONING_TYPE_vd.h.patch"
        "init-VDINTERFACEERROR_vdfuse.c.patch")
md5sums=('1615e64dc1802a8fe3807da238f0d247'
         'fc9038cb274c5207f38378d14572f56e'
         'f7044bcbc724983eb7d8ff60bff4d6e9'
         '03ff94767282bb8b758965a867403401'
         '4e65619e90c9448d6d7eabee0d593c1b')

prepare() {
  [ -d "${srcdir}/includes" ] && rm -rf "${srcdir}/includes" ]
  cp -r "virtualbox-includes-${_headers}" "${srcdir}/includes"
  msg2 "Applying patches..."
  patch -p1 -i "add-typedef-to-PARTITIONING_TYPE_vd.h.patch"
  patch -p1 -i "init-VDINTERFACEERROR_vdfuse.c.patch"
}

build() {
  cd "${srcdir}/debian/${pkgname}"
  gcc "${pkgname}.c" -o "${pkgname}" \
      $(pkg-config --cflags --libs fuse) \
      -I"${srcdir}/includes" \
      -Wl,-rpath -Wl,/usr/lib/virtualbox/ \
      -l:/usr/lib/virtualbox/VBoxDD.so \
      -l:/usr/lib/virtualbox/VBoxDDU.so \
      -Wall $CFLAGS
}

package() {
  install -D -m755 "${srcdir}/debian/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  sed -i '1s,python,&2,' "${srcdir}/vdautomount-0.1.py"
  install -D -m755 "${srcdir}/vdautomount-0.1.py" "${pkgdir}/usr/bin/vdautomount"
}
