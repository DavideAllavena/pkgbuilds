# Maintainer: laloch <lalochcz@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=vdfuse
pkgver=82a
pkgrel=4
pkgdesc="A FUSE module for mounting VirtualBox disk images (VDI/VMDK/VHD) on the host"
arch=('i686' 'x86_64')
url="http://forums.virtualbox.org/viewtopic.php?f=26&t=33355"
license=('GPL3')
depends=('fuse' 'virtualbox' 'svn')
makedepends=('pkgconfig')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/xspect3d/gordstuff/${pkgname}-v${pkgver}.tgz"
	vdautomount-0.1.py::"http://forums.virtualbox.org/download/file.php?id=2865")
md5sums=('7561b2071cde3ac9e3411b5766406c53'
         'f7044bcbc724983eb7d8ff60bff4d6e9')

build() {
  #cd "${srcdir}/${pkgname}-v${pkgver}/include"
  #msg "Updating VBox headers"
  #svn upgrade
  #svn update
  
  cd "${srcdir}/${pkgname}-v${pkgver}"
  msg "Starting make..."
  gcc "${pkgname}-v${pkgver}.c" -o ${pkgname} \
      `pkg-config --cflags --libs fuse` \
      -I"${srcdir}/${pkgname}-v${pkgver}/include" \
      -Wl,-rpath -Wl,/usr/lib/virtualbox/ \
      -l:/usr/lib/virtualbox/VBoxDD.so \
      -l:/usr/lib/virtualbox/VBoxDD2.so \
      -l:/usr/lib/virtualbox/VBoxDDU.so \
      -Wall $CFLAGS

}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  install -D -m755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  sed -i '1s,python,&2,' "$srcdir/vdautomount-0.1.py"
  install -D -m755 "$srcdir/vdautomount-0.1.py" "$pkgdir/usr/bin/vdautomount"
}
