# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Muflone <webreg@vbsimple.net>

pkgname=python2-gudev
_pkgname=python-gudev
pkgver=147.2
pkgrel=4
pkgdesc="Python binding to the GUDev udev helper library"
arch=('any')
url="https://github.com/nzjrs/python-gudev"
license=('LGPL3')
depends=('systemd' 'python2')
source=("https://github.com/nzjrs/${_pkgname}/archive/${pkgver}.tar.gz"
        "configure.patch"
        "makefile.patch")
sha512sums=('aabb342ee944805d4038040242d45f49c9a13d0b92cb435394e9851cab99b6a55e2d7c65cb6291903e8482ccabfce552660520df69919635d20846d8db099583'
	    '19bad2575c3af74fb3a52d99d1b40f3e9e1058455735ac51941100d1fd965323d8f29071c36bc4fa0d03385e27bd807ed61c2849459fe0420332ddcc1952def2'
	    '18a4d3da0c51628c7845ce2a3807c45469005e93563faf800e3ff112093ddfe61c4942fd3a8109050a082bb96af76718423d54b00598837d2213b37c80b4fcfe')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  patch -p1 -i ../configure.patch
  patch -p1 -i ../makefile.patch
}

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  PYTHON=python2 ./autogen.sh --prefix=/usr
  sed -i '/#include <pygobject\.h>/ i#include <python2.7/Python.h>' "gudevmodule.c"
  make
}
package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make install DESTDIR=$pkgdir
}
