# Maintainer: Muflone <muflone@vbsimple.net>

pkgname=gespeaker-git
_gitname=gespeaker
pkgver=0.8.1.46.g6181e3f
pkgrel=2
pkgdesc="A GTK+ frontend for espeak and mbrola to speech the read text."
url="https://github.com/muflone/gespeaker"
arch=('any')
license=('GPL2')
depends=('espeak' 'librsvg' 'pygtk' 'python-dbus-common' 'python2-xdg')
makedepends=('git')
optdepends=('mbrola: for enhanced mbrola voices support'
            'mbrola-voices: at least one mbrola voice')
provides=('gespeaker')
conflicts=('gespeaker')
source=('git://github.com/muflone/gespeaker.git')
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "${_gitname}"
  # Python2 fix
  sed -i 's#env python#env python2#' setup.py
  sed -i 's#env python#env python2#' gespeaker
  sed -i 's#env python#env python2#' src/gespeaker.py
  sed -i 's#env python#env python2#' plugins/plugin_kopete/gespeaker_kopete.py
}

build() {
  cd "${_gitname}"
  python2 setup.py build
}

package() {
  cd "${_gitname}"
  python2 setup.py install --root "${pkgdir}"
}

