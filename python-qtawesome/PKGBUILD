# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgbase='python-qtawesome'
_pkgbase='qtawesome'
pkgname=('python-qtawesome' 'python2-qtawesome')
pkgver=0.5.7
pkgrel=1
pkgdesc='Iconic fonts in PyQt and PySide applications'
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
arch=('any')
url="https://github.com/spyder-ide/${_pkgbase}/"
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('6a9be868b4f9993ca87ac405dac0d82187e16481ee6baf920ab4bee060efcb24')

prepare() {
  # Prepare sources for Python 2.x
  cp -r "${_pkgbase}-${pkgver}" "python2-${_pkgbase}-${pkgver}"
}

build() {
  cd "${_pkgbase}-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/python2-${_pkgbase}-${pkgver}"
  python setup.py build
}

package_python2-qtawesome() {
  depends=('python2-six' 'python2-qtpy')

  cd "${_pkgbase}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-qtawesome() {
  depends=('python-six' 'python-qtpy')

  cd "python2-${_pkgbase}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
