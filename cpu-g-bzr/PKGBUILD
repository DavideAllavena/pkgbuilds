# Maintainer: Muflone <webreg@vbsimple.net>

pkgname=cpu-g-bzr
_pkgname=cpug
pkgver=13
pkgrel=3
pkgdesc="An application that shows useful information about your CPU, Motherboard and some general information about your system."
url="http://cpug.sourceforge.net/"
license=('GPL3')
arch=('any')
source=('bzr+lp:/cpug')
depends=('python' 'pygtk' 'pciutils')
optdepends=('mesa-demos: get information about the graphic driver and OpenGL support')
makedepends=('bzr')
provides=('cpu-g')
conflicts=('cpu-g')
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  bzr revno
}

package() {
  cd "${_pkgname}"
  python2 setup.py install --root=${pkgdir} --prefix=/usr

  # Create further directories (setup.py is not complete enough)
  install -d "${pkgdir}/usr/share/man/man1"

  # Copying files
  cp -r "gfx" "${pkgdir}/usr/share/${_pkgname}"

  # Compress and install the man page
  gzip -cf9 "doc/cpu-g.1" > "doc/cpu-g.1.gz"
  install -Dm644 "doc/cpu-g.1.gz" "${pkgdir}/usr/share/man/man1/"
  [ -e "doc/cpu-g.1.gz" ] && rm "doc/cpu-g.1.gz"
 
  # Rename the main script
  mv "${pkgdir}/usr/bin/cpu-g.py" "${pkgdir}/usr/bin/cpu-g"

  # Fix desktop file
  sed -i 's_Exec=python /usr/bin/cpu-g.py_Exec=python2 /usr/bin/cpu-g_' "${pkgdir}/usr/share/applications/cpu-g.desktop"
}
