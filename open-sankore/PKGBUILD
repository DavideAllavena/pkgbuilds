# Maintainer: Muflone <webreg@vbsimple.net>

pkgname=open-sankore
pkgver=2.00.00
pkgrel=4
pkgdesc="An interactive white board that uses a free standard format"
arch=(i686 x86_64)
url="http://open-sankore.org/en/"
license=(GPL3)
depends=(python2 libpaper qt4 phonon libmysqlclient gstreamer0.10-base qtwebkit libdbusmenu-qt libtiff)
install=package.install

if [ "$CARCH" = "x86_64" ]
then
  source=("http://ftp.open-sankore.org/current/Open-Sankore_Ubuntu_12.04_${pkgver}_amd64.zip")
  md5sums=('78f31b6f4cb9566c0c21a2ffbe0a4f78')
  _debarchive='Open-Sankore_2.00.00_amd64.deb'
else
  source=("http://ftp.open-sankore.org/current/Open-Sankore_Ubuntu_12.04_${pkgver}_i386.zip")
  md5sums=('90228d65e64700d4ea6bbc93853f1746')
  _debarchive='Open-Sankore_2.00.00_i386.deb'
fi


build() {
  ar -x ${_debarchive} data.tar.gz
  tar xvzf data.tar.gz
  # Fix paths from usr/local to usr/share
  sed -i 's#/usr/local/Open-Sankore-2.00.00#/usr/share/open-sankore#g' usr/share/applications/Open-Sankore.desktop
  sed -i 's#/usr/local/Open-Sankore-2.00.00#/usr/share/open-sankore#g' usr/local/Open-Sankore-2.00.00/run.sh
}

package() {
  cd "$srcdir/usr/local/Open-Sankore-$pkgver"
  # Fix invalid permissions
  chmod a+rx library/pictures

  # Install files
  install -d "$pkgdir/usr/share/open-sankore"
  cp -r * "$pkgdir/usr/share/open-sankore"

  # Create a link to the executable
  install -d "$pkgdir/usr/bin"
  ln -s /usr/share/open-sankore/run.sh "$pkgdir/usr/bin/open-sankore"

  install -d "$pkgdir/usr/share/applications"
  install -t "$pkgdir/usr/share/applications" "$srcdir/usr/share/applications/Open-Sankore.desktop"
}

