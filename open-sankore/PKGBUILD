# Maintainer: Muflone http://url.muflone.com/contacts

pkgname=open-sankore
pkgver=2.2.3
pkgrel=1
pkgdesc="An interactive white board that uses a free standard format"
arch=(i686 x86_64)
url="http://open-sankore.org/en/"
license=(GPL3)
depends=(python2 libpaper qt4 phonon libmysqlclient gstreamer0.10-base qtwebkit libdbusmenu-qt libtiff)
install=package.install

if [ "$CARCH" = "x86_64" ]
then
  source=("http://ftp.open-sankore.org/current/Open-Sankore_Ubuntu_12.04_${pkgver}_amd64.zip")
  md5sums=('c77d1ef3c5b97591aba89e62de92496f')
  _debarchive="Open-Sankore_${pkgver}_amd64.deb"
else
  source=("http://ftp.open-sankore.org/current/Open-Sankore_Ubuntu_12.04_${pkgver}_i386.zip")
  md5sums=('ad1b7ca7dd3e90793a15c6b3ff85748a')
  _debarchive="Open-Sankore_${pkgver}_i386.deb"
fi

build() {
  ar -x ${_debarchive} data.tar.gz
  tar xvzf data.tar.gz
  # Fix paths from usr/local to usr/lib
  sed -i "s#/usr/local/Open-Sankore-${pkgver}#/usr/lib/open-sankore#g" usr/share/applications/Open-Sankore.desktop
  sed -i "s#/usr/local/Open-Sankore-${pkgver}#/usr/lib/open-sankore#g" usr/local/Open-Sankore-${pkgver}/run.sh
}

package() {
  cd "$srcdir/usr/local/Open-Sankore-$pkgver"
  # Fix invalid permissions
  chmod a+rx library/pictures

  # Install files
  install -d "$pkgdir/usr/lib/open-sankore"
  cp -r * "$pkgdir/usr/lib/open-sankore"

  # Create a link to the executable
  install -d "$pkgdir/usr/bin"
  ln -s /usr/lib/open-sankore/run.sh "$pkgdir/usr/bin/open-sankore"

  # Install desktop file
  install -d "$pkgdir/usr/share/applications"
  install -t "$pkgdir/usr/share/applications" "$srcdir/usr/share/applications/Open-Sankore.desktop"

  # Install release notes files
  install -d "$pkgdir/usr/share/open-sankore"
  install -m 644 -t "$pkgdir/usr/share/open-sankore" "$srcdir/JournalDesModifications.pdf"
  install -m 644 -t "$pkgdir/usr/share/open-sankore" "$srcdir/ReleaseNotes.pdf"
}

