# Maintainer: Muflone <muflone@vbsimple.net>

pkgname=remmina-plugin-folder
pkgver=1.0.0.0
pkgrel=2
_builderver=1.0.0.2
pkgdesc="A protocol plugin for Remmina to open a folder."
arch=(i686 x86_64)
url="https://github.com/muflone/remmina-plugin-folder"
license=('GPL')
depends=('gtk3' 'remmina' 'hicolor-icon-theme')
makedepends=('pkg-config' 'cmake')
optdepends=(
  'xdg-utils: for Automatically detected option'
  'gnome-open: for Open for GNOME option'
  'nautilus: for Nautilus option'
  'pcmanfm: for PCManFM option'
  'kdebase-dolphin: for Dolphin Browser option'
)
install=${pkgname}.install
source=(
  "remmina-plugin-builder_${_builderver}.tar.gz::https://github.com/muflone/remmina-plugin-builder/archive/${_builderver}.tar.gz"
  "${pkgname}_${pkgver}.tar.gz::https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz"
)
md5sums=(
  'd74a95c501a38774f5422941ed5684e9'
  '201d3a459d48b2bbf071e94c136de0d5'
)

build() {
  [ -d build ] && rm -rf build
  cp -r remmina-plugin-builder-${_builderver} build
  cp -r "${pkgname}-${pkgver}/${pkgname}"/* build/remmina-plugin-to-build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd ${srcdir}/build/
  make DESTDIR=${pkgdir} install
}
