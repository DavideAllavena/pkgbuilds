# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=firefox-theme-adwaita
pkgver=29.1
pkgrel=1
pkgdesc="Adwaita theme for Firefox (Matches the default Gnome Shell theme)"
url="https://addons.mozilla.org/en-US/firefox/addon/adwaita/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('unzip')
source=("${pkgname}-${pkgver}.xpi"::"https://addons.mozilla.org/firefox/downloads/file/254054/gnome_3-${pkgver}-fx.xpi")
md5sums=('7d00cc5ff6a34c97e3f177a41e146666')
noextract=("${pkgname}-${pkgver}.xpi")

prepare() {
  unzip -qo "${pkgname}-${pkgver}.xpi" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Find extension UUID
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  # Prepare destination folder
  _dstdir="${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}"
  install -d "${_dstdir}"
  # Copy files
  cp -R * "${_dstdir}"
  # Restore standard permissions
  find "${pkgdir}" -type d -print0 | xargs --null chmod 0755
  find "${pkgdir}" -type f -print0 | xargs --null chmod 0644
}

