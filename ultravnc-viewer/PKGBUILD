# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=ultravnc-viewer
pkgver=1.2.0.1
_pkgver=${pkgver::-2}
pkgrel=1
pkgdesc="A powerful VNC Client with a lot of features, compatible with Windows' UltraVNC servers"
arch=('any')
url="http://www.uvnc.com"
license=('GPL')
depends=('wine' 'gtk-update-icon-cache')
makedepends=('unzip')
source=("http://www.uvnc.eu/download/${pkgver//./}/vncviewer${pkgver//./}_portable.zip"
        "${pkgname}-16.png"
        "${pkgname}-24.png"
        "${pkgname}-32.png"
        "${pkgname}-48.png"
        "${pkgname}-256.png"
        "${pkgname}"
        "${pkgname}.desktop"
        "${pkgname}-listen.desktop")
sha256sums=('94951b0b39fe0f026b2c743f600e1eb4594ce0c02ff3d6c89860c7b9c2f3f257'
            '5386feaf65fff389db4e4e30950f97d64803815b1e1a219d45bf41c2bde45e21'
            '098876231f8006bb89fc54986175ec6d938fae2c9e4a6dafe9b5c9d36f7fb3a2'
            '48025bb333be1ab9ebf875ced00d98b5701bbd81bad361d34712bc00b2e3c0df'
            '26b29f4a99cc40e3c119eae0f3ae9195d41f57cb2377f1add3e4c771f752aaa2'
            '1106710eab5b5603c15a1c216ad181e60110e8dc17f6442985e24f631b6f5f1b'
            '9a6bc29b1c64a6027c43310453558d259d4ca0960fbd4699f5a3872f9861be9d'
            'c2a3cb12b9fdbd06fc9faf9907a591d7988233dd5e0046ccba625f5f8b3b367d'
            '6eefe94d1dacc0ca414a1e2ff5cc0fb2c272d3743b39240247000e5d03e6d9f2')
install="${pkgname}.install"

package() {
  # Install binary files
  install -m 644 -D "${srcdir}/vncviewer${pkgver//./}_portable.exe" "${pkgdir}/usr/share/${pkgname}/vncviewer.exe"
  # Install startup script
  install -m 755 -D "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  # Install icons
  for _size in 16 24 32 48 256
  do
    install -m 644 -D ${pkgname}-${_size}.png "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  # Install desktop files
  install -m 755 -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${pkgname}.desktop" "${pkgname}-listen.desktop"
}

