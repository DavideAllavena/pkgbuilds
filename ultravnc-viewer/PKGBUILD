# Maintainer: Muflone <muflone@vbsimple.net>
pkgname=ultravnc-viewer
pkgver=1.1.9.0
pkgrel=3
pkgdesc="A powerful VNC Client with a lot of features, compatible with Windows' UltraVNC servers"
arch=(any)
url="http://www.uvnc.com"
license=('GPL')
depends=(wine hicolor-icon-theme gtk-update-icon-cache desktop-file-utils)
makedepends=(unzip)
source=('http://support1.uvnc.com/download/1190/UVNC_1_1_9_w2k_X86.zip'
        'ultravnc-viewer-16.png'
        'ultravnc-viewer-24.png'
        'ultravnc-viewer-32.png'
        'ultravnc-viewer-48.png'
        'ultravnc-viewer-256.png'
        'ultravnc-viewer'
        'ultravnc-viewer.desktop'
        'ultravnc-viewer-listen.desktop')
md5sums=('0ed3f208f67160e30712040aa3d9a62b'
         '4709ef6ec46c16639ddca63c96bc2305'
         'c3a6146aeedd3dc20ad9c742eccd6466'
         '7ec0d88590c885cce8b67cf0728ab9f2'
         'e499e03410e3685b6cdfd6f2aaccc311'
         '7259762f1237e40ae2a8e4bfb6e7a1ab'
         'a15ff58b88db0e7837ebbd3a088a055a'
         'bed00f59f63426c791b2fb1c4ebb4fa7'
         '2ee68f7a88d001993b97edd543738403')
install="$pkgname.install"

package() {
  # Install binary files
  install -m 644 -D "$srcdir/w2k_X86/installer/vncviewer.exe" "$pkgdir/usr/share/${pkgname}/vncviewer.exe"
  # Install startup script
  install -m 755 -D "ultravnc-viewer" "$pkgdir/usr/bin/ultravnc-viewer"
  # Install icons
  install -m 644 -D ultravnc-viewer-16.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/ultravnc-viewer.png"
  install -m 644 -D ultravnc-viewer-24.png "$pkgdir/usr/share/icons/hicolor/24x24/apps/ultravnc-viewer.png"
  install -m 644 -D ultravnc-viewer-32.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/ultravnc-viewer.png"
  install -m 644 -D ultravnc-viewer-48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/ultravnc-viewer.png"
  install -m 644 -D ultravnc-viewer-256.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/ultravnc-viewer.png"
  # Install desktop files
  install -m 644 -D ultravnc-viewer.desktop "$pkgdir/usr/share/applications/ultravnc-viewer.desktop"
  install -m 644 -D ultravnc-viewer-listen.desktop "$pkgdir/usr/share/applications/ultravnc-viewer-listen.desktop"
}

