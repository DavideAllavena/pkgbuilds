# Maintainer: Muflone <muflone@vbsimple.net>

pkgname=iseriesaccess5
pkgver=5.4.0.1.2
pkgrel=1
_filever='5.4.0-1.2'
pkgdesc="IBM iSeries Access provides a 5250 emulator and others tools for IBM iSeries (formerly known as AS400)."
arch=(i686 x86_64)
url="http://www-03.ibm.com/systems/power/software/i/access/linux.html"
license=('custom')
makedepends=(zenity)
depends=(unixodbc xorg-fonts-75dpi xorg-fonts-100dpi libstdc++5 desktop-file-utils)
optdepends=('rpm: diagnostic package information for cwbmedic')
provides=(iseriesaccess)
conflicts=(iseriesaccess)
install=package.install
source=(
        "file://iSeriesAccess-${_filever}.i386.rpm"
        "iseriesaccess_i686.conf"
        "iseriesaccess_x86_64.conf"
        "ibm5250"
        "ibm5250.png"
        "ibm5250.desktop"
        "setup5250"
        "setup5250.png"
        "setup5250.desktop"
       )
md5sums=(
         '5587ac7938a9c310d7ee88c3a7ead6ba'
         '346745e55dca69c03bda3a5e9a6931e1'
         'e5bddef5fef091cc01c222cfafa49a8b'
         'a57e8e4776c40ad5fd24d9b4f955552d'
         '58703c74e4908fbc4df41bc32e35ab51'
         '85bf8a1c091f59ef5357429161db1514'
         'ba3acf9b4cecc66301ca1fd3e34e469a'
         '8b9a1d6ab9513699a95ababab85ba3c2'
         'f14cbb1cd9d72a29808978a64e1852e0'
        )
DLAGENTS=(
          "${DLAGENTS[@]}"
          'local::/bin/true'
          'file::/bin/ln -s "$(zenity --file-selection --title="Select resource for %u" --file-filter $(basename %u))" %o'
         )
_bindir='bin'

if [ "${CARCH}" = 'x86_64' ]
then
  source+=("file://iSeriesAccess-${_filever}.x86_64.rpm")
  md5sums+=('e18874a8cbb4c2a0781a0bef545112c3')
  depends+=(lib32-libstdc++5 lib32-libxpm lib32-libxmu lib32-libxp lib32-openmotif lib32-unixodbc)
  _bindir='bin64'
fi

package() {
  msg2 "Install files for the package"
  cp -r opt "${pkgdir}"
  install -d "${pkgdir}/etc/ld.so.conf.d"
  install -m 644 -t "${pkgdir}/etc/ld.so.conf.d/" iseriesaccess_${CARCH}.conf

  msg2 "Create symlinks for executable"
  install -d "${pkgdir}/usr/bin"
  for file in cwbcopwr cwbping cwbrunsql cwbtrc rmtcmd rmtodbc
  do
    ln -s /opt/ibm/iSeriesAccess/${_bindir}/${file} "${pkgdir}/usr/bin/"
  done
  install -m 755 -t "${pkgdir}/usr/bin" setup5250
  install -m 755 -t "${pkgdir}/usr/bin" ibm5250

  msg2 "Install license file"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /opt/ibm/iSeriesAccess/doc/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/"

  msg2 "Install icons"
  install -d "$pkgdir/usr/share/pixmaps"
  install -m 644 -t "$pkgdir/usr/share/pixmaps" ibm5250.png
  install -m 644 -t "$pkgdir/usr/share/pixmaps" setup5250.png

  msg2 "Install desktop files"
  install -d "$pkgdir/usr/share/applications"
  install -m 644 -t "$pkgdir/usr/share/applications" ibm5250.desktop
  install -m 644 -t "$pkgdir/usr/share/applications" setup5250.desktop
}

