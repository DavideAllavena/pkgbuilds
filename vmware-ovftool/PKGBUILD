# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=vmware-ovftool
pkgver=4.0.0.2301625
pkgrel=1
arch=('i686' 'x86_64')
url="https://www.vmware.com/support/developer/ovf/"
license=('custom:vmware')
depends=('curl')
_pkgmainver=${pkgver%.*}
_pkgsubver=${pkgver##*.}

if [ "$CARCH" = "x86_64" ]
then
  _pkgarch=$CARCH
  sha256sums=('a75e830e252915fd0d3ed20bc148cb4a5eb4f838b02baa24f14297b19e018ce9')
else
  _pkgarch=i386
  sha256sums=('d5f6fd979f8cab3a265205ae71e22b17562c1aee82bb231e9e431068debc80f0')
fi
source=("file://VMware-ovftool-${_pkgmainver}-${_pkgsubver}-lin.${_pkgarch}.bundle")
pkgdesc="VMware Open Virtualization Format tool (place the file VMware-ovftool-${_pkgmainver}-${_pkgsubver}-lin.YOUR-ARCH.bundle in the same directory of the PKGBUILD)"

build() {
  rm -rf "${srcdir}/build"
  sh "../VMware-ovftool-${_pkgmainver}-${_pkgsubver}-lin.${_pkgarch}.bundle" -x "${srcdir}/build"
}

package() {
  cd "${srcdir}/build/${pkgname}"
  # Install binaries files
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgname}" icudt44l.dat lib*
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}" ovftool ovftool.bin
  # Install data files
  for _subdir in certs env env/en schemas/DMTF schemas/vmware
  do
    install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/${_subdir}"
    install -m 644 -t "${pkgdir}/usr/lib/${pkgname}/${_subdir}" "${_subdir}"/*.*
  done
  # Install main script symlink
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/ovftool" "${pkgdir}/usr/bin/ovftool"
  # Install license files
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    vmware.eula vmware-eula.rtf open_source_licenses.txt
  # Install documentation files
  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.txt
}

