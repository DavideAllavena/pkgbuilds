# Maintainer: Muflone <webreg@vbsimple.net>

pkgname=bluewho-git
pkgver=0.2
pkgrel=1
pkgdesc="Information and notification of new discovered bluetooth devices"
url="http://bluewho.muflone.com/"
arch=('any')
license=('GPL2')
depends=('gtk3' 'gobject-introspection' 'python2-xdg' 'python2-gobject' 'python2-pybluez' 'gtk-update-icon-cache')
optdepends=('libcanberra: to play notification sound using canberra-gtk-play'
            'alsa-utils: to play notification sound using aplay'
            'libpulse: to play notification sound using paplay'
            'mplayer: to play notification sound using mplayer'
           )
provides=('bluewho')
conflicts=('bluewho')
source=("git://github.com/muflone/bluewho.git")
md5sums=('SKIP')
install="${pkgname}.install"

pkgver() {
  cd "${pkgname%-*}"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${pkgname%-*}"
  python2 setup.py build
}

package() {
  cd "${pkgname%-*}"
  python2 setup.py install --optimize=1 --root "${pkgdir}"
}

