# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj
pkgver=1.0.26.201903251040
pkgrel=1
pkgdesc='DBeaver plugin for SSHv2 tunnels with Bouncy Castle algorithm'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-sshj-lib')
source=("https://dbeaver.jkiss.org/update/sshj/latest/plugins/org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
        "https://dbeaver.jkiss.org/update/sshj/latest/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('00c001c65d55d802aa62b1ec42f18fa48b23b7ac112cc325c8b7f33ed6383aa2'
            'b68f9cf34a18f9b93fc1d38a82d2af28ac727d6603d99c80fee52aa56c289ea1'
            '047d873e3eef0ccfba3f0991c9e9414bc83cc378ab98a588fc4f8be9532dbc9d')
noextract=("org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
           "org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
