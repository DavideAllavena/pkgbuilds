# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj
pkgver=1.0.33.201907061627
pkgrel=1
pkgdesc='DBeaver plugin for SSHv2 tunnels with Bouncy Castle algorithm'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-sshj-lib')
source=("https://dbeaver.io/update/sshj/latest/plugins/org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
        "https://dbeaver.io/update/sshj/latest/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('7a0c34d55898a72ea3b59f80f5a8cb650351b6500812090525cfc2457f5c9ebf'
            '1b6cfe58ca81716a28a0292d78b1b0e5ac6e8078de568d9ceed39a49a579fc19'
            'e522d67fc51ba568b1809d3482f7fbdf63dc61a315d923233098a6d97b502230')
noextract=("org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
           "org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
