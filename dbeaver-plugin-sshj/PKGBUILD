# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj
pkgver=1.0.36.201908191349
pkgrel=1
pkgdesc='DBeaver plugin for SSHv2 tunnels with Bouncy Castle algorithm'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-sshj-lib')
source=("https://dbeaver.io/update/sshj/latest/plugins/org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
        "https://dbeaver.io/update/sshj/latest/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('bb75fced5a74a2f624b2fad8218227a80f785bece9855a37573fa4a5872f2546'
            '8cbf57577da07fc04a3217f707e546368ece77706dedff9986cb79cba30411a0'
            'f28509ee79af0cd37d68de990f90b442a3dfc54a901f38fed6eda742322def73')
noextract=("org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
           "org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
