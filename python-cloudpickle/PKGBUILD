# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Chris Warrick <aur@chriswarrick.com>

pkgbase=python-cloudpickle
pkgname=('python-cloudpickle' 'python2-cloudpickle')
_pyname=cloudpickle
pkgver=1.2.1
pkgrel=1
pkgdesc='Extended pickling support for Python objects'
arch=('any')
url='https://github.com/cloudpipe/cloudpickle'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('603244e0f552b72a267d47a7d9b347b27a3430f58a0536037a290e7e0e212ecf')

prepare() {
  # Prepare sources for python2 build
  cp -r "${_pyname}-${pkgver}" "python2-${_pyname}-${pkgver}"
}

build() {
  cd "${_pyname}-${pkgver}"
  python3 setup.py build

  cd "${srcdir}/python2-${_pyname}-${pkgver}"
  python2 setup.py build
}

package_python-cloudpickle() {
  depends=('python-setuptools')

  cd "${_pyname}-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cloudpickle() {
  depends=('python2-setuptools')

  cd "python2-${_pyname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

