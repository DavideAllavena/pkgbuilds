# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=perl-gtk3-simplelist
_perl_module=Gtk3-SimpleList
pkgver=0.20
pkgrel=1
pkgdesc="Perl interface to the Gtk3's complex MVC list widget"
arch=('any')
url="https://metacpan.org/release/${_perl_module}"
license=('LGPL')
depends=('perl-gtk3')
source=("https://cpan.metacpan.org/authors/id/T/TV/TVIGNAUD/${_perl_module}-${pkgver}.tar.gz")
sha512sums=('148db613044342eb4536779f099f597827139b8551e6a306d749128b3c39c6797ed360eee830d60dd6d6f2d134c142683c34dac6eb09fc6b30ec5483f3f49f6c')
options=('!emptydirs')

build() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
