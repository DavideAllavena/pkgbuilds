# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=xca
pkgver=2.2.0
pkgrel=1
pkgdesc='Graphical user interface to OpenSSL, RSA/DSA public keys, certificates, signing requests and revokation lists'
arch=('x86_64')
url='https://hohnstaedt.de/xca/'
license=('BSD')
depends=('qt5-tools' 'libltdl' 'desktop-file-utils' 'gtk-update-icon-cache')
source=("https://github.com/chris2511/${pkgname}/releases/download/RELEASE.${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a0506364820145b3bcef3241538346ab6f4ba0deffb9893c605c22445c4b5a3c83cb9a277db3753beb916498120d2a668b88ef7f4aa349065c092a3d5f87d03a')

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --with-openssl
  sed -i  -r 's/(^LDFLAGS.*$)/LDFLAGS='"'"'-lstdc++'"'"'/' Local.mak
}

package() {
  cd "${pkgname}-${pkgver}"
  make install destdir="${pkgdir}"
  install -m 644 -D COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}
