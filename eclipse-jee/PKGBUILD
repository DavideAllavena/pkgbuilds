# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Giuliano Schneider <gs93@gmx.net>
# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

_eclipse_name=kepler
_eclipse_release=SR2
pkgname=eclipse-jee
pkgver=4.3.2
pkgrel=2
pkgdesc="Eclipse IDE for Java EE Developers"
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=('EPL')
depends=('java-environment>=6' 'gtk2' 'xulrunner' 'webkitgtk2')
provides=("eclipse=${pkgver}")
conflicts=('eclipse')
install="${pkgname}.install"
sha256sums=('8e0351281fd3e4e6ab1fe4e7676ca16b0fa974a1d4e5e4a9409d3732ebfdf411'
            '4cca2873697a3af39a96449021d7fdc2fc2b01abc9f7883946081f6be7a5ed48'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            '4a86578ebbe8adddf731b6bef31c05277ea792cb22d733b6a5c0a25f65546caf'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14')
if [ "$CARCH" = 'x86_64' ]; then
  sha256sums[0]='dce7a19877c27c1d4e1ec43ec8356bab03142d3f95d243d5d5ad3cfcba133c4c'
  _arch="-${CARCH}"
fi
source=("${pkgname}-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz"::"http://www.eclipse.org/downloads/download.php?file=/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/${pkgname}-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz&r=1"
        "eclipse.sh"
        "eclipse.desktop"
        "eclipse.ini.patch"
        "eclipse.svg")
# prevent package compression
PKGEXT=".pkg.tar"

build() {
  patch -p1 -i "eclipse.ini.patch"
}

package() {
  # Copy application files
  install -m 755 -d "${pkgdir}/usr/share"
  cp -a "${srcdir}/eclipse" "${pkgdir}/usr/share"

  # Copy executable file
  install -m 755 -d "${pkgdir}/usr/bin"
  install -m 755 eclipse.sh "${pkgdir}/usr/bin/eclipse"

  # Copy icons
  local _icon_path="/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v20140221-1700"
  install -m755 -d "${pkgdir}/usr/share/icons"
  for _size in 16 32 48 256
  do
    install -m755 -d "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps"
    ln -s "${_icon_path}/eclipse${_size}.png" "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/eclipse.png"
  done
  install -m 755 -d "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
  install -m 644 "${srcdir}/eclipse.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/eclipse.svg"

  # Copy desktop file
  install -m 755 -d "${pkgdir}/usr/share/applications"
  install -m 755 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/eclipse.desktop"
}
